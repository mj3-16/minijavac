plugins {
    id 'com.github.sherter.google-java-format' version '0.5'
}

apply plugin: 'application'

mainClassName = "minijava.Main"

// policy is to have a 'build' script; that requires us to have a non-default build directory
buildDir = 'build_dir'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

googleJavaFormat {
    toolVersion '1.1'
    exclude 'mjtest'
    exclude 'jFirm'
    exclude 'libfirm'
}

project(':jFirm').ant.importBuild 'build.xml'
// the task jFirm:jar makes sure that libfirm.so exists
tasks.compileJava.dependsOn project(':jFirm').jar
// added to the run task and the generated start scripts
applicationDefaultJvmArgs = ["-Djna.library.path=${projectDir}/jFirm/lib"]
// set library path in the jvm that executes the tests
tasks.withType(Test) { systemProperty 'jna.library.path', 'jFirm/lib' }

dependencies {
    compile files('jFirm/lib/jna.jar', 'jFirm/classes/jfirm.jar')
    compile 'com.beust:jcommander:1.58'
    compile 'com.google.guava:guava:19.0'
    compile 'commons-io:commons-io:2.4'
    compile 'com.intellij:annotations:5.1'
    compile 'org.jooq:jool:0.9.9'
    compile 'org.pcollections:pcollections:2.1.2'
    testCompile 'com.github.npathai:hamcrest-optional:1.0'
    testCompile 'com.pholser:junit-quickcheck-core:0.6.1'
    testCompile 'com.pholser:junit-quickcheck-generators:0.6.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.google.jimfs:jimfs:1.1'
    testCompile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
}

task mjtest(type: Exec) {
    workingDir './mjtest'
    commandLine 'python3', './mjt.py', 'all', '../run', '--ci_testing', '--parallel'
}

tasks.check.dependsOn mjtest

tasks.test.dependsOn installDist
tasks.mjtest.dependsOn installDist

task installGitHooks(type: Copy) {
    from file('git-hooks/pre-commit')
    into { file('.git/hooks') }
}
